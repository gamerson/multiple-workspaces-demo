allprojects {
	repositories {
		maven {
			url "http://localhost:8081/nexus/content/groups/public/"
		}
	}

	group = "com.test"
}

dependencies {
	targetPlatformBoms group: "com.test", name: "commons", version: "1.0.0-SNAPSHOT"
}

subprojects {
	pluginManager.withPlugin("com.liferay.osgi.plugin") {
		apply plugin: "maven-publish"

		publishing {
			repositories {
				maven {
					credentials {
						username = "admin"
						password = "admin123"
					}

					url "http://localhost:8081/nexus/content/repositories/snapshots"
				}
			}

			publications {
				mavenJava(MavenPublication) {
					from components.java
				}
			}
		}

		afterEvaluate { project ->
			if (deploy.enabled) {
				deploy.doLast {
					configurations.default.resolvedConfiguration.resolvedArtifacts.each { artifact ->
						if (artifact.extension == 'jar') {
							logger.lifecycle "Deploying required dependency file {}", artifact
							project.copy {
								from artifact.file
								into project.deploy.destinationDir
								rename { "${artifact.name}.${artifact.extension}" }
							}
						}
					}
				}
			}
		}
	}
}